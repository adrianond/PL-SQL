create or replace PACKAGE BODY PCK_PROPOSTA_CREDITO IS

PROCEDURE PRC_CRIA_PROPOSTA(P_PROPOSTA IN PROPOSTA_CREDITO_TYPE) IS

V_PROPOSTA_TYPE PROPOSTA_CREDITO_TYPE:= P_PROPOSTA;
V_SEQ_ENDERECO ENDERECO_PROPOSTA_CREDITO.ID%TYPE;
V_SEQ_CLIENTE CLIENTE_PROPOSTA_CREDITO.ID%TYPE;

-- PRAGMA EXCEPTION_INIT - usa o codigo -20001 para lançar e capturar uma ENDERECO_EXCEPTION
ENDERECO_EXCEPTION EXCEPTION;
PRAGMA EXCEPTION_INIT(ENDERECO_EXCEPTION, -20001);

BEGIN
 SELECT SEQ_ENDERECO_PROPOSTA_CREDITO.NEXTVAL INTO V_SEQ_ENDERECO FROM DUAL;
 V_PROPOSTA_TYPE.CLIENTE.ENDERECO.ID := V_SEQ_ENDERECO;
 
 SELECT SEQ_CLIENTE_PROPOSTA_CREDITO.NEXTVAL INTO V_SEQ_CLIENTE FROM DUAL;
 V_PROPOSTA_TYPE.CLIENTE.ID := V_SEQ_CLIENTE;

 PRC_POPULA_ENDERECO(V_PROPOSTA_TYPE.CLIENTE.ENDERECO);
 PRC_POPULA_CLIENTE(V_PROPOSTA_TYPE.CLIENTE);
 PRC_POPULA_TELEFONE(V_PROPOSTA_TYPE.CLIENTE.TELEFONES, V_SEQ_CLIENTE);
 PRC_POPULA_PROPOSTA(V_PROPOSTA_TYPE);
 
 COMMIT;
 
  EXCEPTION WHEN ENDERECO_EXCEPTION THEN
    ROLLBACK;
    PRC_LOG_ERROR('PCK_PROPOSTA_CREDITO', 'PRC_CRIA_PROPOSTA', 'SQL CODE:' || ' ' || SQLCODE || ' ' || 'ERROR:' || ' ' || SQLERRM);
  WHEN OTHERS THEN
    ROLLBACK;
    PRC_LOG_ERROR('PCK_PROPOSTA_CREDITO', 'PRC_CRIA_PROPOSTA', 'SQL CODE: ' || SQLCODE || ' ' || 'ERROR: ' || SQLERRM);
    
END PRC_CRIA_PROPOSTA;

PROCEDURE PRC_POPULA_ENDERECO(P_ENDERECO IN ENDERECO_PROPOSTA_CREDITO_TYPE) IS

BEGIN
 INSERT INTO ENDERECO_PROPOSTA_CREDITO(
   ID,
   ENDERECO,
   NUMERO,
   COMPLEMENTO,
   CEP,
   BAIRRO,
   CIDADE,
   UF
 )
 values(
   P_ENDERECO.ID,
   P_ENDERECO.ENDERECO, 
   P_ENDERECO.NUMERO, 
   P_ENDERECO.COMPLEMENTO, 
   P_ENDERECO.CEP, 
   P_ENDERECO.BAIRRO, 
   P_ENDERECO.CIDADE, 
   P_ENDERECO.UF
   );
   
 EXCEPTION WHEN OTHERS THEN
  RAISE_APPLICATION_ERROR(-20001, 'ERRO AO INSERIR ENDEREÇO!' || ' ' || 'SQLCODE: ' || SQLCODE || ' ' || 'ERROR' || SQLERRM);
 
END PRC_POPULA_ENDERECO;

PROCEDURE PRC_POPULA_CLIENTE(P_CLIENTE IN CLIENTE_PROPOSTA_CREDITO_TYPE) IS

BEGIN
INSERT INTO CLIENTE_PROPOSTA_CREDITO(
  ID,
  NOME,
  CPF,
  RG,
  DATA_NASCIMENTO,
  id_endereco
  ) VALUES (
  P_CLIENTE.ID,
  P_CLIENTE.NOME,
  P_CLIENTE.CPF,
  P_CLIENTE.RG,
  P_CLIENTE.DATANASCIMENTO,
  P_CLIENTE.ENDERECO.ID
  );
  
  EXCEPTION WHEN OTHERS THEN
  RAISE;
  
END PRC_POPULA_CLIENTE;

PROCEDURE PRC_POPULA_TELEFONE(P_TELEFONES IN TELEFONE_PROPOSTA_CREDITO_TYPE_TABLE, ID_CLIENTE IN NUMBER) IS

V_SEQ_TELEFONE NUMBER;

BEGIN
 FOR I IN 1 .. P_TELEFONES.COUNT LOOP
    SELECT SEQ_TELEFONE_PROPOSTA_CREDITO.NEXTVAL INTO V_SEQ_TELEFONE FROM DUAL;
   
   INSERT INTO TELEFONE_PROPOSTA_CREDITO(
    ID,
    TIPO,
    NUMERO,
    DDD,
    ID_CLIENTE
   ) VALUES (
   V_SEQ_TELEFONE,
   P_TELEFONES(I).TIPO,
   P_TELEFONES(I).NUMERO,
   P_TELEFONES(I).DDD,
   ID_CLIENTE
   );
 END LOOP;

 EXCEPTION WHEN OTHERS THEN
  RAISE_APPLICATION_ERROR(-20002, 'ERRO AO INSERIR TELEFONES!' || ' ' || 'SQLCODE: ' || SQLCODE || ' ' || 'ERROR' || SQLERRM);
 
 END PRC_POPULA_TELEFONE;

PROCEDURE PRC_POPULA_PROPOSTA(P_PROPOSTA IN PROPOSTA_CREDITO_TYPE) IS

V_SEQ_PROPOSTA NUMBER;

BEGIN
SELECT SEQ_PROPOSTA_CREDITO.NEXTVAL INTO V_SEQ_PROPOSTA FROM DUAL;
  INSERT INTO PROPOSTA_CREDITO(
   ID,
   PRODUTO,
   VALOR,
   STATUS,
   ID_CLIENTE
  ) VALUES (
   V_SEQ_PROPOSTA,
   P_PROPOSTA.PRODUTO,
   P_PROPOSTA.VALOR,
   P_PROPOSTA.STATUS,
   P_PROPOSTA.CLIENTE.ID
  );

  EXCEPTION WHEN OTHERS THEN
  RAISE_APPLICATION_ERROR(-20002, 'ERRO AO POPULAR TABELA PROPOSTA_CREDITO' || ' ' || 'SQLCODE: ' || SQLCODE || ' ' || 'ERROR' || SQLERRM);

END PRC_POPULA_PROPOSTA;

END PCK_PROPOSTA_CREDITO;